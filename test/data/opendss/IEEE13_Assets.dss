// Reproduced from OpenDSS
// OpenDSS License
//* Copyright (c) 2008-2021, Electric Power Research Institute, Inc.
//* All rights reserved.
//*
//* Redistribution and use in source and binary forms, with or without
//* modification, are permitted provided that the following conditions are met:
//*     * Redistributions of source code must retain the above copyright
//*       notice, this list of conditions and the following disclaimer.
//*     * Redistributions in binary form must reproduce the above copyright
//*       notice, this list of conditions and the following disclaimer in the
//*       documentation and/or other materials provided with the distribution.
//*     * Neither the name of the Electric Power Research Institute, Inc., nor
//*       the names of its contributors may be used to endorse or promote
//*       products derived from this software without specific prior written
//*       permission.
//*
//* THIS SOFTWARE IS PROVIDED BY Electric Power Research Institute, Inc., "AS IS"
//* AND ANY EXPRESS OR IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE
//* IMPLIED WARRANTIES OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE
//* DISCLAIMED. IN NO EVENT SHALL Electric Power Research Institute, Inc., OR ANY OTHER
//* ENTITY CONTRIBUTING TO OR INVOLVED IN THE PROVISION OF THE SOFTWARE, BE
//* LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL, SPECIAL, EXEMPLARY, OR
//* CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED TO, PROCUREMENT OF
//* SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR PROFITS; OR BUSINESS
//* INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF LIABILITY, WHETHER IN
//* CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING NEGLIGENCE OR OTHERWISE)
//* ARISING IN ANY WAY OUT OF THE USE OF THIS SOFTWARE, EVEN IF ADVISED OF THE
//* POSSIBILITY OF SUCH DAMAGE.

Clear

// added some CapControls and AssetInfosheets to test the CDPSM export

new circuit.IEEE13NodecktAssets
~ basekv=115 pu=1.00 phases=3 bus1=SourceBus
~ Angle=30                     ! advance angle 30 deg so result agree with published angle
~ MVAsc3=20000 MVASC1=21000    ! stiffen the source to approximate inf source

New XfmrCode.SubXfmr Phases=3 Windings=2  XHL=(8 1000 /)
~ wdg=1 conn=delta kv=115  kva=5000  %r=(.5 1000 /)
~ wdg=2 conn=wye   kv=4.16 kva=5000  %r=(.5 1000 /)
New XfmrCode.FdrXfmr Phases=3   Windings=2  XHL=2  %NoLoadLoss=0.6 %imag=1.1
~ wdg=1 conn=Wye kv=4.16   kva=500   %r=.55
~ wdg=2 conn=Wye kv=0.480  kva=500   %r=.55
New XfmrCode.RegLeg  phases=1 xhl=0.01 kvas=[1666 1666] kvs=[2.4 2.4] %LoadLoss=0.01

//////////////////////////////////////////////////////////////////

new WireData.ACSR_556_5  NormAmps=730 DIAM=0.927  GMRac=0.37320  Rdc=0.035227273   Runits=kft Radunits=in gmrunits=in
new WireData.ACSR_4/0    NormAmps=340 DIAM=0.563  GMRac=0.09768  Rdc=0.112121212   Runits=kft Radunits=in gmrunits=in
new WireData.ACSR_1/0    NormAmps=230 DIAM=0.398  GMRac=0.05352  Rdc=0.212121212   Runits=kft Radunits=in gmrunits=in
new WireData.CU_1/0 NormAmps=100 DIAM=0.368  GMRac=0.13356 Rac=0.607 Runits=mi Radunits=in gmrunits=in
new TSData.TS_1/0   NormAmps=165 DIAM=0.368  GMRac=0.13320 Rac=0.97  Runits=mi Radunits=in gmrunits=in
~                   EpsR=2.3 InsLayer=0.220 DiaIns=0.82 DiaCable=1.06 DiaShield=0.88 TapeLayer=0.005 TapeLap=20
new CNData.CN_250   NormAmps=260 DIAM=0.567  GMRac=0.20520 Rac=0.41  Runits=mi Radunits=in gmrunits=in
~                   EpsR=2.3 InsLayer=0.220 DiaIns=1.06 DiaCable=1.29 k=13 DiaStrand=0.0641 GmrStrand=0.02496 Rstrand=14.8722

new LineSpacing.500 nconds=4 nphases=3 units=ft x=[-4 -1 3 0] h=[28 28 28 24]
new LineSpacing.505 nconds=3 nphases=2 units=ft x=[-4 3 0] h=[28 28 24]
new LineSpacing.510 nconds=2 nphases=1 units=ft x=[0.5 0] h=[29 24]

new LineGeometry.601 nconds=4 nphases=3 reduce=y spacing=500 wires=[ACSR_556_5 ACSR_556_5 ACSR_556_5 ACSR_4/0]
new LineGeometry.602 nconds=4 nphases=3 reduce=y spacing=500 wires=[ACSR_4/0   ACSR_4/0   ACSR_4/0   ACSR_4/0]
new LineGeometry.603 nconds=3 nphases=2 reduce=y spacing=505 wires=[ACSR_1/0   ACSR_1/0   ACSR_1/0]
new LineGeometry.604 like=603
new LineGeometry.605 nconds=2 nphases=1 reduce=y spacing=510 wires=[ACSR_1/0   ACSR_1/0]

new LineGeometry.606 nconds=3 nphases=3 reduce=y
~ cond=1 cncable=CN_250 x=-0.5 h=-4 units=ft
~ cond=2 cncable=CN_250 x= 0.0 h=-4 units=ft
~ cond=3 cncable=CN_250 x= 0.5 h=-4 units=ft

new LineGeometry.607 nconds=2 nphases=1 reduce=y
~ cond=1 tscable=TS_1/0 x= 0.0000 h=-4 units=ft
~ cond=2 wire   =CU_1/0 x= 0.2500 h=-4 units=ft

//////////////////////////////////////////////////////////////////

New Transformer.Sub  XfmrCode=SubXfmr Buses=[SourceBus 650]
New Transformer.XFM1 XfmrCode=FdrXfmr Buses=[633 634]

New Transformer.Reg1 XfmrCode=RegLeg Bank=Reg Buses=[650.1 RG60.1]
New Transformer.Reg2 XfmrCode=RegLeg Bank=Reg Buses=[650.2 RG60.2]
New Transformer.Reg3 XfmrCode=RegLeg Bank=Reg Buses=[650.3 RG60.3]

new regcontrol.Reg1  transformer=Reg1 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9
new regcontrol.Reg2  transformer=Reg2 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9
new regcontrol.Reg3  transformer=Reg3 winding=2  vreg=122  band=2  ptratio=20 ctprim=700  R=3   X=9

New Load.671 Bus1=671.1.2.3       Phases=3 Conn=Delta Model=1 kV=4.16   kW=1155 kvar=660
New Load.634a Bus1=634.1   Phases=1 Conn=Wye  Model=1 kV=0.277  kW=160   kvar=110
New Load.634b Bus1=634.2   Phases=1 Conn=Wye  Model=1 kV=0.277  kW=120   kvar=90
New Load.634c Bus1=634.3   Phases=1 Conn=Wye  Model=1 kV=0.277  kW=120   kvar=90
New Load.645 Bus1=645.2    Phases=1 Conn=Wye  Model=1 kV=2.4      kW=170   kvar=125
New Load.646 Bus1=646.2.3    Phases=1 Conn=Delta Model=2 kV=4.16    kW=230   kvar=132
New Load.692 Bus1=692.3.1    Phases=1 Conn=Delta Model=5 kV=4.16    kW=170   kvar=151
New Load.675a Bus1=675.1  Phases=1 Conn=Wye  Model=1 kV=2.4  kW=485   kvar=190
New Load.675b Bus1=675.2  Phases=1 Conn=Wye  Model=1 kV=2.4  kW=68   kvar=60
New Load.675c Bus1=675.3  Phases=1 Conn=Wye  Model=1 kV=2.4  kW=290   kvar=212
New Load.611 Bus1=611.3    Phases=1 Conn=Wye  Model=5 kV=2.4  kW=170   kvar=80
New Load.652 Bus1=652.1    Phases=1 Conn=Wye  Model=2 kV=2.4  kW=128   kvar=86
New Load.670a Bus1=670.1   Phases=1 Conn=Wye  Model=1 kV=2.4  kW=17    kvar=10
New Load.670b Bus1=670.2   Phases=1 Conn=Wye  Model=1 kV=2.4  kW=66    kvar=38
New Load.670c Bus1=670.3   Phases=1 Conn=Wye  Model=1 kV=2.4  kW=117  kvar=68

!Bus 670 is the concentrated point load of the distributed load on line 632 to 671 located at 1/3 the distance from node 632

New Line.650632    Phases=3 Bus1=RG60.1.2.3   Bus2=632.1.2.3  Geometry=601 Length=2000 units=ft
New Line.632670    Phases=3 Bus1=632.1.2.3    Bus2=670.1.2.3  Geometry=601 Length=667  units=ft
New Line.670671    Phases=3 Bus1=670.1.2.3    Bus2=671.1.2.3  Geometry=601 Length=1333 units=ft
New Line.671680    Phases=3 Bus1=671.1.2.3    Bus2=680.1.2.3  Geometry=601 Length=1000 units=ft
New Line.632633    Phases=3 Bus1=632.1.2.3    Bus2=633.1.2.3  Geometry=602 Length=500  units=ft
New Line.632645    Phases=2 Bus1=632.3.2      Bus2=645.3.2    Geometry=603 Length=500  units=ft
// New Line.645646    Phases=2 Bus1=645.3.2      Bus2=646.3.2    Geometry=603 Length=300  units=ft
New Line.645646    Phases=2 Bus1=645.3.2      Bus2=646.3.2    Length=0.0568  units=mi normamps=230
~ rmatrix = (1.3238 | 0.2066 1.3294 )
~ xmatrix = (1.3569 | 0.4591 1.3471 )
New Line.692675    Phases=3 Bus1=692.1.2.3    Bus2=675.1.2.3  Geometry=606 Length=500  units=ft
New Line.671684    Phases=2 Bus1=671.1.3      Bus2=684.1.3    Geometry=604 Length=300  units=ft
// New Line.684611    Phases=1 Bus1=684.3        Bus2=611.3      Geometry=605 Length=300  units=ft
New Line.684611    Phases=1 Bus1=684.3 Bus2=611.3 Spacing=510 Wires=[ACSR_1/0 ACSR_1/0] Length=300  units=ft
New Line.684652    Phases=1 Bus1=684.1        Bus2=652.1      Geometry=607 Length=800  units=ft
New Line.671692    Phases=3 Bus1=671   Bus2=692  Switch=y  r1=1e-4 r0=1e-4 x1=0.000 x0=0.000 c1=0.000 c0=0.000

New Capacitor.Cap1 Bus1=675 phases=3 kVAR=600 kV=4.16
New Capacitor.Cap2 Bus1=611.3 phases=1 kVAR=100 kV=2.4
New CapControl.Cap1 capacitor=cap1 type=time on=8 off=19 element=capacitor.cap1
New CapControl.Cap2 capacitor=cap2 type=voltage on=115 off=125 ptratio=20 ptphase=1 element=line.684611 terminal=2


Set Voltagebases=[115, 4.16, .48]
calcv
Solve
//BusCoords IEEE13Node_BusXY.csv

Transformer.Reg1.Taps=[1.0 1.0625]
Transformer.Reg2.Taps=[1.0 1.0500]
Transformer.Reg3.Taps=[1.0 1.06875]
Set Controlmode=OFF

Solve
